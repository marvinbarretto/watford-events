apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

def getVersionNameFromPackageJson() {
    def packageJson = file('../../package.json')
    def packageJsonText = packageJson.text
    def packageJsonObj = new groovy.json.JsonSlurper().parseText(packageJsonText)
    return packageJsonObj.version
}

def getVersionCodeFromPackageJson() {
    def version = getVersionNameFromPackageJson()
    // Convert "0.0.2" to integer like 002
    def parts = version.split('\\.')
    def major = parts[0] as Integer
    def minor = parts[1] as Integer
    def patch = parts[2] as Integer
    return major * 10000 + minor * 100 + patch
}

android {
    namespace "app.web.watfordevents"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "app.web.watfordevents"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        // AUTO-SYNC VERSIONS:
        versionCode getVersionCodeFromPackageJson()
        versionName getVersionNameFromPackageJson()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')


    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:33.16.0')

    // TODO: Add the dependencies for Firebase products you want to use
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation 'com.google.firebase:firebase-analytics'

  // Add the dependencies for any other desired Firebase products
  // https://firebase.google.com/docs/android/setup#available-libraries

    // Existing test dependencies...
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
